apply plugin: 'osgi'

configurations {
    ajtools
    ajcTestCompile.extendsFrom testCompile
    ajcTestRuntime.extendsFrom testRuntime
    ajc
}


sourceSets {
    ajcTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }

    ajc {
        def main = sourceSets.main
        java.srcDirs = main.java.srcDirs
        resources.srcDirs = main.resources.srcDirs
        compileClasspath = main.compileClasspath
        runtimeClasspath = main.runtimeClasspath
    }
}



compileAjcTestJava {

    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    doLast {
        ant.taskdef(resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", classpath: configurations.ajtools.asPath)
        ant.iajc(source: "1.8", target: "1.8",
                destDir: "${sourceSets.ajcTest.output.classesDir.absolutePath}", maxmem: "512m", fork: "true",
                classpath: "${sourceSets.main.output.classesDir.absolutePath};${configurations.testCompile.asPath}")
                {
                    sourceroots {
                        files("src/ajcTest/java/com/netflix/hystrix/contrib/javanica/test/aspectj", "src/test/java/com/netflix/hystrix/contrib/javanica/test/common", "src/main/java/com/netflix/hystrix/contrib/javanica/aop/aspectj").each {
                            File file -> pathelement(location: file.absolutePath)
                        }
                    }
                }
    }

}

compileAjcJava {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    doLast {
        ant.taskdef(resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", classpath: configurations.ajtools.asPath)
        ant.iajc(source: "${sourceCompatibility}", target: "${targetCompatibility}",
                destDir: "${sourceSets.ajc.output.classesDir.absolutePath}", maxmem: "512m", fork: "true", "showWeaveInfo": "true",
                classpath: "${configurations.compile.asPath}")

                {
                    sourceroots {
                        sourceSets.ajc.java.srcDirs.each {
                            pathelement(location: it.absolutePath)

                        }
                    }
                }
    }
}

task ajcTest(type: Test) {
    testClassesDir = sourceSets.ajcTest.output.classesDir
    classpath = sourceSets.ajcTest.runtimeClasspath
    if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-Xdebug',
                '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=9009'
    }
}

check.dependsOn test, ajcTest

task ajcJar(type: Jar) {
    archiveName = "${project.name}" + "-ctw-${version}.jar"
    destinationDir = file("${buildDir}/libs")
    from sourceSets.ajc.output
}

assemble.dependsOn(jar, ajcJar)

dependencies {
    compileApi project(':collapserx1-core')
    ajtools libraries.aspectjtools
    testRuntime libraries.aspectjrt
    compileApi libraries.aspectjweaver
    compile libraries.aspectjrt

    compileApi libraries.guava
    compile libraries.commonsLang
    compileApi libraries.jsr305
    compile libraries.asm
    testCompile libraries.junit
    testCompile libraries.junitparams
    testCompile project(':collapserx1-junit')
    testCompile libraries.springCore
    testCompile libraries.springContext
    testCompile libraries.springAop
    testCompile libraries.springTest
    testCompile libraries.cglib
    testCompile libraries.mockitoCore
    testCompile libraries.log4j
    testCompile libraries.slf4jLog4j
    testCompile libraries.junitDataprovider
}
